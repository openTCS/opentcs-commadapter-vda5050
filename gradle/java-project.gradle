apply plugin: 'java'
apply plugin: 'org.kordamp.gradle.stats'
apply plugin: 'com.diffplug.spotless'

base.archivesName = name.toLowerCase()

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'

  compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
  testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.12.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.12.0'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.12.0'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.12.0'

  testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.16.0'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
  testImplementation group: 'com.approvaltests', name: 'approvaltests', version: '24.17.0'

  testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.17'
}

compileJava {
  options.release = 21
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

compileTestJava {
  options.release = 21
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
}

javadoc {
  title = "openTCS VDA5050 Communication Adapter ${project.version} API documentation: ${project.name}"

  options {
    header = "openTCS VDA5050 Communication Adapter ${project.version}"
    addBooleanOption('Xdoclint:none', true)
  }
}
test {
  useJUnitPlatform()
  // ignoreFailures = true
  systemProperties.put("java.awt.headless", "true")
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

spotless {
  java {
    // Use the default import order configuration
    importOrder()

    // Use the Eclipse JDT formatter
    eclipse('4.26').configFile("${rootDir}/config/eclipse-formatter-preferences.xml")
  }
}
